import groovy.json.JsonSlurper

buildscript {
    def versionFile = file('versions.json')
    def json = new JsonSlurper().parseText(versionFile.text)

    ext {
        gdxVersion = json.libGDXVersion
        roboVMVersion = json.roboVMVersion
        androidGradleToolsVersion = json.androidGradleToolsVersion
        androidBuildToolsVersion = json.androidBuildToolsVersion
        androidSDKVersion = Integer.parseInt(json.androidSDKVersion)
    }    

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:' + androidGradleToolsVersion
        classpath 'org.robovm:robovm-gradle-plugin:' + roboVMVersion
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"    

    repositories {
        jcenter()
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    }
}

project(":core") {
    apply plugin: "java"

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    dependencies {
        compile "net.engio:mbassador:1.2.0"
        compile "org.xerial:sqlite-jdbc:3.7.2"
        testCompile 'junit:junit:4.12'
    }
}

project(":android") {
    apply plugin: 'com.android.application'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    android {
        buildToolsVersion androidBuildToolsVersion
        compileSdkVersion androidSDKVersion

        defaultConfig {
            applicationId "org.robovm.samples.contractr.android"
            minSdkVersion 9
            targetSdkVersion 23
            versionCode 1
            versionName "1.0"
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_1_6
            targetCompatibility JavaVersion.VERSION_1_6
        }

        lintOptions {
            abortOnError false
        }

        buildTypes {
            release {
                minifyEnabled false
                proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            }
        }
    }

    dependencies {
        compile project(":core")        
        compile 'org.sqldroid:sqldroid:1.0.3'
        compile 'org.roboguice:roboguice:2.0'                
        compile 'com.android.support:appcompat-v7:22.1.1'
        compile fileTree(dir: 'libs', include: ['*.jar'])
    }
}

project(":ios") {
    apply plugin: "java"
    apply plugin: "robovm"

    sourceCompatibility = 1.8
    targetCompatibility = 1.8
    
    dependencies {        
        compile project(":core")
        compile group: 'org.robovm', name: 'robovm-rt', version: roboVMVersion
        compile group: 'org.robovm', name: 'robovm-cocoatouch', version: roboVMVersion
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.6'
}